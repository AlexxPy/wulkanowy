buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'org.greenrobot:greendao-gradle-plugin:3.2.2'
        classpath 'io.fabric.tools:gradle:1.25.1'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'io.fabric'
apply from: '../jacoco.gradle'
apply from: '../android-sonarqube.gradle'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "io.github.wulkanowy"
        testApplicationId "io.github.tests.wulkanowy"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "0.1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        buildConfigField "String", "UPDATE_URL", "\"https://bitrise-redirector.herokuapp.com" +
                "/v0.1/apps/daeff1893f3c8128/builds/" + "${getCurrentGitBranch()}" + "/artifacts/app-release-bitrise-signed.apk/info\""
        manifestPlaceholders = [
                fabricApiKey: System.getenv("FABRIC_API_KEY") ?: "null"
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled = true
            ext.enableCrashlytics = false
        }
    }

    testOptions {
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }
}

greendao {
    schemaVersion 19
    generateTests = true
}

dependencies {
    implementation project(':api')
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support:support-v4:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    implementation 'com.android.support:cardview-v7:27.0.2'
    implementation 'com.android.support:customtabs:27.0.2'
    implementation 'com.firebase:firebase-jobdispatcher:0.8.5'
    implementation 'com.thoughtbot:expandablerecyclerview:1.3'
    implementation 'org.apache.commons:commons-lang3:3.7'
    implementation 'eu.davidea:flexible-adapter:5.0.0-rc3'
    implementation 'org.apache.commons:commons-collections4:4.1'
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'com.jakewharton:butterknife:8.8.1'
    implementation 'joda-time:joda-time:2.9.9'
    implementation 'com.github.javiersantos:AppUpdater:2.6.4'
    implementation 'com.google.dagger:dagger-android:2.14.1'
    implementation 'com.google.dagger:dagger-android-support:2.14.1'

    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true
    }
    implementation('com.crashlytics.sdk.android:answers:1.4.1@aar') {
        transitive = true
    }

    annotationProcessor 'com.google.dagger:dagger-android-processor:2.14.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    debugImplementation 'com.amitshekhar.android:debug-db:1.0.1'
    debugImplementation 'net.zetetic:android-database-sqlcipher:3.5.9'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.13.0'

    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'org.mockito:mockito-android:2.13.0'
}

static def getCurrentGitBranch() {
    if (gitBranch() == "HEAD") {
        return System.getenv("BITRISE_GIT_BRANCH")
    }

    return gitBranch()
}

static def gitBranch() {
    def branch = "null"
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}
