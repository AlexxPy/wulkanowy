buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'org.greenrobot:greendao-gradle-plugin:3.2.2'
        classpath 'io.fabric.tools:gradle:1.25.1'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'io.fabric'
apply from: '../jacoco.gradle'
apply from: '../android-sonarqube.gradle'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.3"
    defaultConfig {
        applicationId "io.github.wulkanowy"
        testApplicationId "io.github.tests.wulkanowy"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 4
        versionName "0.2.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        manifestPlaceholders = [
                fabricApiKey: System.getenv("FABRIC_API_KEY") ?: "null"
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            testCoverageEnabled = true
            ext.enableCrashlytics = false
        }
    }

    testOptions {
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }
}

greendao {
    schemaVersion 22
    generateTests = true
}

dependencies {
    implementation project(':api')
    implementation "com.android.support:support-v4:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:cardview-v7:$supportVersion"
    implementation "com.android.support:customtabs:$supportVersion"
    implementation "com.firebase:firebase-jobdispatcher:$firebaseJob"
    implementation "org.apache.commons:commons-lang3:$apacheLang"
    implementation "org.apache.commons:commons-collections4:$apacheCollections"
    implementation "eu.davidea:flexible-adapter:$flexibleAdapter"
    implementation "eu.davidea:flexible-adapter-ui:$flexibleUi"
    implementation "org.greenrobot:greendao:$greenDao"
    implementation "com.github.yuweiguocn:GreenDaoUpgradeHelper:$greenDaoHelper"
    implementation "com.jakewharton:butterknife:$butterknife"
    implementation "joda-time:joda-time:$jodaTime"
    implementation "com.google.dagger:dagger-android-support:$dagger2"
    implementation "com.aurelhubert:ahbottomnavigation:$ahbottom"

    implementation("com.crashlytics.sdk.android:crashlytics:$crashlyticsSdk@aar") {
        transitive = true
    }
    implementation("com.crashlytics.sdk.android:answers:$crashlyticsAnswers@aar") {
        transitive = true
    }

    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger2"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger2"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife"

    debugImplementation "com.amitshekhar.android:debug-db:$debugDb"
    debugImplementation "net.zetetic:android-database-sqlcipher:$sqlcipher"

    testImplementation "junit:junit:$junit"
    testImplementation "org.mockito:mockito-core:$mockito"

    androidTestImplementation "com.android.support.test:runner:$testRunner"
    androidTestImplementation "org.mockito:mockito-android:$mockito"
}
