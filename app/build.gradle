apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'com.github.triplet.play'
apply from: 'jacoco.gradle'
apply from: 'sonarqube.gradle'

def fabricApiKey = System.getenv("FABRIC_API_KEY") ?: "null"

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    playAccountConfigs {
        defaultAccountConfig {
            serviceAccountEmail = System.getenv("PLAY_SERVICE_ACCOUNT_EMAIL")
            pk12File = file('key.p12')
        }
    }

    defaultConfig {
        applicationId "io.github.wulkanowy"
        testApplicationId "io.github.tests.wulkanowy"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 16
        versionName "0.5.2"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        playAccountConfig = playAccountConfigs.defaultAccountConfig
        manifestPlaceholders = [ fabricApiKey: fabricApiKey ]
    }

    signingConfigs {
        release {
            storeFile file("upload-key.jks")
            storePassword System.getenv("PLAY_STORE_PASSWORD")
            keyAlias System.getenv("PLAY_KEY_ALIAS")
            keyPassword System.getenv("PLAY_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            buildConfigField "boolean", "FABRIC_ENABLED", "true"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            buildConfigField "boolean", "FABRIC_ENABLED", fabricApiKey == "null" ? "false" : "true"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            testCoverageEnabled = true
            ext.enableCrashlytics = fabricApiKey != "null" && !project.hasProperty("disableCrashlytics")
            multiDexKeepProguard file('proguard-multidex-rules.pro')
        }
    }

    lintOptions {
        disable 'HardwareIds'
    }
}

androidExtensions {
    experimental = true
}

play {
    track = 'alpha'
    uploadImages = true
}

ext.androidx_version = "1.0.0"

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation('com.github.wulkanowy:api:e829b094de') { exclude module: "threetenbp" }

    implementation "androidx.legacy:legacy-support-v4:$androidx_version"
    implementation "androidx.appcompat:appcompat:$androidx_version"
    implementation "androidx.cardview:cardview:$androidx_version"
    implementation "com.google.android.material:material:$androidx_version"
    implementation 'androidx.multidex:multidex:2.0.0'

    implementation 'com.takisoft.preferencex:preferencex:1.0.0'
    implementation "com.mikepenz:aboutlibraries:6.2.0"
    implementation "com.firebase:firebase-jobdispatcher:0.8.5"

    //Do not update dagger https://github.com/google/dagger/issues/1245
    implementation "com.google.dagger:dagger-android-support:2.16"
    kapt "com.google.dagger:dagger-compiler:2.16"
    kapt "com.google.dagger:dagger-android-processor:2.16"

    implementation "androidx.room:room-runtime:2.1.0-alpha02"
    implementation "androidx.room:room-rxjava2:2.1.0-alpha02"
    kapt "androidx.room:room-compiler:2.1.0-alpha02"

    implementation "eu.davidea:flexible-adapter:5.1.0"
    implementation "eu.davidea:flexible-adapter-ui:1.0.0"

    implementation "com.aurelhubert:ahbottomnavigation:2.2.0"
    implementation 'com.ncapdevi:frag-nav:3.0.0-RC3'

    implementation 'com.github.pwittchen:reactivenetwork-rx2:3.0.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation "io.reactivex.rxjava2:rxjava:2.2.2"

    implementation "com.jakewharton.threetenabp:threetenabp:1.1.0"

    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation "at.favre.lib:slf4j-timber:1.0.1"
    implementation("com.crashlytics.sdk.android:crashlytics:2.9.5@aar") {
        transitive = true
    }
    implementation("com.crashlytics.sdk.android:answers:1.4.3@aar") {
        transitive = true
    }

    debugImplementation "com.amitshekhar.android:debug-db:1.0.4"

    testImplementation "junit:junit:4.12"
    testImplementation "io.mockk:mockk:1.8.9"
    testImplementation "org.mockito:mockito-inline:2.23.0"
    testImplementation 'org.threeten:threetenbp:1.3.7'

    androidTestImplementation 'androidx.test:core:1.0.0'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.ext:junit:1.0.0'
    androidTestImplementation "org.mockito:mockito-android:2.23.0"

    androidTestImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}
